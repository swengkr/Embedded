ESP32-C3 Core ëª¨ë“ˆê³¼ PN-532 NFC Readerë¥¼ ê¸°ë°˜ìœ¼ë¡œ ì„œë³´ ëª¨í„° ì œì–´ ë°ëª¨ë¥¼ ì œì‘í–ˆìŠµë‹ˆë‹¤.

### êµ¬ì„± ìš”ì†Œ
- MCU ëª¨ë“ˆ : ESP32-C3 Core (WeAct Studio)
- NFC Reader : PN-532 (I2C)
- ì •ì „ì‹ ìŠ¤ìœ„ì¹˜ : HW-763 (ê°ë„ ë³´ì •: 20pF ì½˜ë´ì„œ) â‡’ ì¹´ë“œ ë“±ë¡ ëª¨ë“œ ì‹œì‘/ì·¨ì†Œ/ì¢…ë£Œ
- ì˜¤ë””ì˜¤ ì¬ìƒ : DFPlayer Mini
- ì˜¤ë””ì˜¤ íŒŒì¼ : MP3 (Generated by TTS)
- ë„ì–´ ê°œí : SG-90 Servo

> ğŸ’¬ ì´ë¯¸ì§€ í´ë¦­ ì‹œ ìœ íˆ¬ë¸Œ ì˜ìƒ ì¬ìƒ

[![](https://github.com/swengkr/Embedded/blob/main/ESP32/NFC/project.jfif)](https://youtu.be/9dj0bzYEIGw)

[![](https://github.com/swengkr/Embedded/blob/main/ESP32/NFC/project2.jfif)](https://youtu.be/9dj0bzYEIGw)

### íšŒë¡œ ê²°ì„ ë„
[![](https://github.com/swengkr/Embedded/blob/main/ESP32/NFC/wiring.png)](https://youtu.be/9dj0bzYEIGw)

### ì•„ë‘ì´ë…¸ ë¼ì´ë¸ŒëŸ¬ë¦¬
[![](https://github.com/swengkr/Embedded/blob/main/ESP32/NFC/library.png)](https://youtu.be/9dj0bzYEIGw)

### ESP32-C3 ì†ŒìŠ¤ ì½”ë“œ(Arduino)
```cpp
#include <Wire.h>
#include <Arduino.h>
#include <Adafruit_PN532.h>
#include "DFRobotDFPlayerMini.h"
#include <ESP32Servo.h>
#include <SHA2Builder.h>

// ----------------------------------------------------
// í•€ ì •ì˜
// ----------------------------------------------------
#define PN532_SDA (2) // ì˜ˆì‹œ: GPIO 2
#define PN532_SCL (3) // ì˜ˆì‹œ: GPIO 3
#define DF_TXD    (4)
#define DF_RXD    (5)
#define DF_BUSY   (6) // DFPlayer BUSY í•€ ì—°ê²° (LOW = ì¬ìƒ ì¤‘)
#define SWITCH    (7)
#define SERVO     (9)
#define LED       (10)

// PN532 ê°ì²´ ìƒì„±: IRQì™€ RESET í•€ì€ ì‚¬ìš©í•˜ì§€ ì•Šìœ¼ë¯€ë¡œ -1ë¡œ ì„¤ì •
// I2C í•€ì€ Wire.begin()ì„ í†µí•´ ì§€ì •ë˜ë¯€ë¡œ -1ë¡œ ì²˜ë¦¬í•©ë‹ˆë‹¤.
Adafruit_PN532 pn532(-1, -1); 
HardwareSerial hwSerial(1);
DFRobotDFPlayerMini dfPlayer;
Servo servo;

int8_t sysState = 0;
uint8_t uid[2][7];
uint8_t regCards = 0;
uint8_t newCards = 0;
uint8_t regUID[5][7];
bool ledState = false;
unsigned long triggerTime = 0;
TaskHandle_t nfcTaskHandle;

// NFC ì½ê¸° íƒœìŠ¤í¬
void nfcTask(void *parameter) {
  while (true) {
    bool isPlaying = (digitalRead(DF_BUSY) == LOW);

    if (!isPlaying) {
      uint8_t length;
      bool exists = false;

      // ì¹´ë“œë¥¼ ê°ì§€í•˜ê³  ì½ìŠµë‹ˆë‹¤. (ì¹´ë“œê°€ ê°ì§€ë  ë•Œê¹Œì§€ ëŒ€ê¸°í•˜ê±°ë‚˜ íƒ€ì„ì•„ì›ƒë©ë‹ˆë‹¤)
      if (pn532.readPassiveTargetID(PN532_MIFARE_ISO14443A, uid[0], &length)) {
        triggerTime = millis();

        Serial.print("NFC ì¹´ë“œ ê°ì§€(");
        Serial.print(length, DEC);
        Serial.print("):");

        // UID ê°’ì„ 16ì§„ìˆ˜ë¡œ ì¶œë ¥
        for (uint8_t i = 0; i < length; i++) {
          Serial.print(" 0x");
          Serial.print(uid[0][i], HEX);
        }
        Serial.println();

        for (uint8_t index = 0; index < regCards; ++index) {
          if (regUID[index][0] == uid[0][0] &&
            regUID[index][1] == uid[0][1] &&
            regUID[index][2] == uid[0][2] &&
            regUID[index][3] == uid[0][3]) {
              exists = true;
              if (sysState == 0) {
                // ì˜¤ë””ì˜¤: ë¬¸ì´ ì—´ë ¸ìŠµë‹ˆë‹¤.
                dfPlayer.play(6);
                Serial.println("ì˜¤ë””ì˜¤: ë¬¸ì´ ì—´ë ¸ìŠµë‹ˆë‹¤");
                for (int pos = 125; pos >= 20; pos -= 5) { 
                  servo.write(pos);
                  delay(10);
                }
                delay(1000);
                for (int pos = 20; pos <= 125; pos += 5) { 
                  servo.write(pos);
                  delay(10);
                }
              } else if (sysState == 1 || sysState == 2) {
                // ì˜¤ë””ì˜¤: ì´ë¯¸ ë“±ë¡í•œ ì¹´ë“œì…ë‹ˆë‹¤.
                dfPlayer.play(9);
                Serial.println("ì˜¤ë””ì˜¤: ì´ë¯¸ ë“±ë¡í•œ ì¹´ë“œì…ë‹ˆë‹¤");
              }
              break;
            }
        }
        if (!exists) {
          if (sysState == 1 || sysState == 2) {
            if (regCards == 5) {
              // ì˜¤ë””ì˜¤: ë” ì´ìƒ ì¹´ë“œë¥¼ ë“±ë¡í•  ìˆ˜ ì—†ìŠµë‹ˆë‹¤
              dfPlayer.play(11);
              Serial.println("ì˜¤ë””ì˜¤: ë” ì´ìƒ ì¹´ë“œë¥¼ ë“±ë¡í•  ìˆ˜ ì—†ìŠµë‹ˆë‹¤");
            } else {
              // ìƒˆ ì¹´ë“œ ID ë³µì‚¬
              memcpy(regUID[regCards++], uid[0], sizeof(uid[0]));
              // ì˜¤ë””ì˜¤: ìƒˆ ì¹´ë“œë¥¼ ë“±ë¡í–ˆìŠµë‹ˆë‹¤
              dfPlayer.play(10);
              Serial.println("ì˜¤ë””ì˜¤: ìƒˆ ì¹´ë“œë¥¼ ë“±ë¡í–ˆìŠµë‹ˆë‹¤");
              ++newCards;
            }
          } else {
            // ì˜¤ë””ì˜¤: ë¯¸ë“±ë¡ ì¹´ë“œì…ë‹ˆë‹¤.
            dfPlayer.play(7);
            Serial.println("ì˜¤ë””ì˜¤: ë¯¸ ë“±ë¡ ì¹´ë“œì…ë‹ˆë‹¤");
          }
          vTaskDelay(pdMS_TO_TICKS(300));
        }
      }
      continue;
    }
    vTaskDelay(pdMS_TO_TICKS(100));
  }
}

void setup(void) {
  pinMode(DF_BUSY, INPUT);
  pinMode(SWITCH, INPUT);
  pinMode(LED, OUTPUT);

  Serial.begin(115200);
  Serial.println("ì¶œì…ê´€ë¦¬ì‹œìŠ¤í…œ ì‹œì‘");

  // eFuse MAC ì½ê¸° (ì¹© ê³ ìœ ê°’)
  uint64_t chipid = ESP.getEfuseMac();
  uint8_t mac[6];
  char macStr[18];  // "AA:BB:CC:DD:EE:FF" + null = 18 bytes

  for (int i = 0; i < 6; i++) {
    mac[i] = (chipid >> (8 * (5 - i))) & 0xFF;
  }

  snprintf(macStr, sizeof(macStr), "%02X:%02X:%02X:%02X:%02X:%02X", mac[0], mac[1], mac[2], mac[3], mac[4], mac[5]);

  // Create SHA-256 (default hash size)
  SHA2Builder sha2_256;
  sha2_256.begin();
  sha2_256.add(macStr);
  sha2_256.calculate();
  String hash_256 = sha2_256.toString();
  Serial.printf("Base MAC: %s (%s)\n", macStr, hash_256.c_str());

  servo.attach(SERVO);
  servo.write(130);

  // I2C ë²„ìŠ¤ ì´ˆê¸°í™”: PN532 ê°ì²´ ì´ˆê¸°í™”ë³´ë‹¤ ë¨¼ì € í˜¸ì¶œë˜ì–´ì•¼ í•©ë‹ˆë‹¤.
  Wire.begin(PN532_SDA, PN532_SCL); 

  // PN532 ë¼ì´ë¸ŒëŸ¬ë¦¬ ì´ˆê¸°í™”
  pn532.begin();

  uint32_t versiondata = pn532.getFirmwareVersion();
  if (!versiondata) {
    Serial.println("PN532 ëª¨ë“ˆì„ ì°¾ì„ ìˆ˜ ì—†ìŠµë‹ˆë‹¤. ì—°ê²°ì„ í™•ì¸í•˜ì„¸ìš”!");
    while (1); // ëª¨ë“ˆì„ ì°¾ì„ ë•Œê¹Œì§€ ë¬´í•œ ëŒ€ê¸°
  }

  // íŒì›¨ì–´ ë²„ì „ ì •ë³´ ì¶œë ¥
  Serial.print("ë””ë°”ì´ìŠ¤ ì°¾ìŒ: PN5"); 
  Serial.print((versiondata>>24) & 0xFF, HEX);
  Serial.print(" (íŒì›¨ì–´: "); 
  Serial.print((versiondata>>16) & 0xFF, DEC); 
  Serial.print('.'); 
  Serial.print((versiondata>>8) & 0xFF, DEC);
  Serial.println(")");

  // ì¼ë°˜ì ì¸ ì¹´ë“œ ë¦¬ë” ëª¨ë“œë¡œ ì„¤ì •
  pn532.SAMConfig();

  hwSerial.begin(9600, SERIAL_8N1, DF_RXD, DF_TXD);
  // DF Player Mini ì´ˆê¸°í™”(reset í•´ì œ => íŒ ë…¸ì´ì¦ˆ ì œê±°)
  if (!dfPlayer.begin(hwSerial, true, false)) {
    Serial.println("DFPlayer Mini ì—°ê²° ì‹¤íŒ¨");
    while (true);
  }

  dfPlayer.volume(25);
  // ì˜¤ë””ì˜¤: ì¶œì…ê´€ë¦¬ì‹œìŠ¤í…œì„ ì‹œì‘í•©ë‹ˆë‹¤.
  dfPlayer.play(1);

  // NFC ì½ê¸°ìš© íƒœìŠ¤í¬ ìƒì„±
  xTaskCreatePinnedToCore(
    nfcTask,         // íƒœìŠ¤í¬ í•¨ìˆ˜
    "NFCTask",       // ì´ë¦„
    4096,            // ìŠ¤íƒ í¬ê¸°
    NULL,            // ë§¤ê°œë³€ìˆ˜
    1,               // ìš°ì„ ìˆœìœ„
    &nfcTaskHandle,  // í•¸ë“¤
    0                // ì½”ì–´ (ESP32-C3ëŠ” ë‹¨ì¼ì½”ì–´ì§€ë§Œ í˜¸í™˜ ìœ„í•´ ëª…ì‹œ)
  );
}

void loop(void) {
  if (sysState == 0) {
    if (digitalRead(SWITCH) == HIGH) {
      if (triggerTime == 0) {
        triggerTime = millis();
      } else if (millis() - triggerTime > 1000) {
        triggerTime = millis();
        sysState = 1; // ë„¤íŠ¸ì›Œí¬ ì„¤ì • ì‹œì‘
        // ì˜¤ë””ì˜¤: ì¹´ë“œ ë“±ë¡ì„ ì‹œì‘í•©ë‹ˆë‹¤.
        dfPlayer.play(8);
        Serial.println("ì¹´ë“œ ë“±ë¡ ì‹œì‘");
        newCards = 0;
      }
    } else {
      triggerTime = 0;
    }
  } else {
    digitalWrite(LED, ledState ^= true);
    if (sysState == 1) {
      if (digitalRead(SWITCH) == LOW) {
        sysState = 2;
      }
    } else if (sysState == 2) {
      int8_t state = 1;
      if (millis() - triggerTime > 60000) {
        state = -1; // ì¹´ë“œ ë“±ë¡ ì‹œê°„ ì´ˆê³¼
      } else if (digitalRead(SWITCH) == HIGH) {
        state = -2; // ì¹´ë“œ ë“±ë¡ ì·¨ì†Œ
      }
      if (state != 1) {
        sysState = 0;
        triggerTime = 0;
        if (0 < newCards) {
          state = 2;
        }
        Serial.printf("ì¹´ë“œ ë“±ë¡ %s\n", state == 2 ? "ì¢…ë£Œ" : state == -1 ? "ì‹œê°„ ì´ˆê³¼" : "ì·¨ì†Œ");
        digitalWrite(LED, ledState = false);
      }
    }
  }
  delay(100);
}
```

### TTS ìŒì› ìƒì„± ì†ŒìŠ¤ ì½”ë“œ (Python)
```python
import asyncio
#Microsoft Edge TTS ì‚¬ìš©
import edge_tts

TEXT = "ìƒì„±í•  í…ìŠ¤íŠ¸ë¥¼ ì…ë ¥í•˜ì„¸ìš”."
#VOICE = "ko-KR-InJoonNeural"  # í•œêµ­ì–´ ë‚¨ì„± ìŒì„±
VOICE = "ko-KR-SunHiNeural"  # í•œêµ­ì–´ ì—¬ì„± ìŒì„±
OUTPUT = "tts_output.mp3"

async def main():
    communicate = edge_tts.Communicate(TEXT, VOICE)
    await communicate.save(OUTPUT)
    print(f"[ì™„ë£Œ] {OUTPUT} íŒŒì¼ ìƒì„±ë¨")

asyncio.run(main())
```
